apply plugin: "digital.wup.android-maven-publish"

task androidSourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.sourceFiles
}

task androidJavadocs(type: Javadoc) {
	source += files(android.sourceSets.main.java.srcDirs)
	if (kotlinLanguage && (android.sourceSets.main.kotlin.srcDirs != null)) {
	    source += files(android.sourceSets.main.kotlin.srcDirs)
	}
	options {
	    linksOffline 'https://developer.android.com/reference/', "file:///${android.sdkDirectory}/docs/reference/"
	}
	exclude '**/*.cpp', '**/*.groovy', '**/*.kt', '**/*.md', '**/R.html', '**/R.*.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
	classifier = 'javadoc'
	from androidJavadocs.destinationDir
}

publishing {
	publications {
		mavenAar(MavenPublication) {
			from components.android

			artifact androidSourcesJar
			artifact androidJavadocsJar

			pom {
				group = GROUP
				artifactId = ARTIFACT_ID
				version = VERSION_NAME
			}
		}
	}
	repositories {
		maven {
			url = 'https://jitpack.io'
			credentials { username authToken }
		}
	}
}

repositories {
	maven {
		url "https://jitpack.io"
		credentials { username authToken }
	}
}
